!BM--------------------------
Module initTIS

Contains

  !BS------------------------
  subroutine init_TIS
  use inputpar 
  use system_module
  use timestep_module
  use phase_module
  use path_module
  use pot_module
  use dyn_module
  use initMD
  use TIS_module 
  use mdstep
  use orderparameter
  use propagator
  use randomnize
  use output_module
  use TISstep
  use convert
  use restart_module
  use types;use assign_objects;use alloc_objects 
  implicit none
  type(timeslice_type)::timeslice1, timeslice2,wt1,wt2
  type(path_type)::trajsegf,trajsegb,trial,wp1,wp2
  type(phasexv_type)::phasexv 
  double precision::op1,op2
  logical::reverseTF
  integer::LMAX,LPB,LPF,LPATH
  double precision::opmin,opmax
  integer::iopmin,iopmax ,i,dwc
  logical::RESTART0,allowmaxlength,skip_create_initial_point
  character::startcon

  print *,"INITIALIZE (PP)TIS PARAMETERS"

  !make a lot of allocations
  dwc=dim+1
  call alloc(TIS_param,Npart)
  call alloc(timeslice1,Npart,dim,Nthermo,NOPS,NWANNIER,dwc)
  call alloc(timeslice2,Npart,dim,Nthermo,NOPS,NWANNIER,dwc)
  call alloc(phasexv,Npart,dim)
  call alloc(trajsegf,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(trajsegb,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(trial,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(startpath,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(wp1,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(wp2,Npart,dim,Nthermo,NOPS,NX,NWANNIER,dwc)
  call alloc(wt1,Npart,dim,Nthermo,NOPS,NWANNIER,dwc)
  call alloc(wt2,Npart,dim,Nthermo,NOPS,NWANNIER,dwc)
 
  !call the MD initialization routine without RESTART option 
  RESTART0=RESTART;RESTART=.false.
  if (RESTART0) then
    call init_MD(skip_create_initial_point)
  else
    call init_MD
  endif
  !Restore the original RESTART value .true./.flase.
  RESTART=RESTART0

  !Save some parameters into the output-object
  output%PATH_FILE="PATH.dat"
  output%IUPATH=2000
  open(output%IUPATH,file=output%PATH_FILE,position="append")
  output%skipp=skipp
  output%PATHINFO=PATHINFO

  !Check if the interface positions make some sense
  if ((INTERFACEL>INTERFACER).OR.(INTERFACEL>INTERFACEM).OR. &
      (INTERFACEM>INTERFACER)) then
    print *,"ERROR init_TIS: POSITION INTERFACES"
    stop
  endif

  !Save the startcondition. Path should either start at the left
  !(TIS A->B), at the right (TIS B->A) or both sides are OK (PPTIS) 
  !!PPTIS has not been checked yet!!
  select case(startcondition)
    case("LEFT")
      startcon="L"
    case("RIGHT")
      startcon="R"
    case("BOTH")
      startcon=" "
    case default
      print *,"initTIS ERROR startcondition=",startcondition
      stop
  end select
  TIS_param%startcondition=startcon

  !set some TIS parameters
  TIS_param%INTERFACEL=INTERFACEL
  TIS_param%INTERFACEM=INTERFACEM
  TIS_param%INTERFACER=INTERFACER
  TIS_param%NX=NX
  TIS_param%NOPS=NOPS
  if (sigdp>=0.d0) then
    TIS_param%sigdp_sqrtm=sigdp/sqrt(syst%masses)
    TIS_param%aimless=.false.
  else 
    TIS_param%sigdp_sqrtm=0.d0
    TIS_param%aimless=.true.
  endif
  TIS_param%timerevfreq=timerevfreq
  TIS_param%biasv=biasv


  if (RESTART) then     !restart using the latest saved trajectory
    call readRestartPATH(output%icyclestart,startpath,restartfile,syst,DYNAMICS)
    if (RENUM) then 
      output%icyclestart=0
      startpath%index_acc=0
      startpath%index_shoot=0
    endif
  else
    !generate a path from scratch using the startpoint 
    !(generated by initmd) as initial point
    output%icyclestart=0
    !prepare the complete timeslice
    call convert_phasep2tslice(startpoint,syst,pot,dyn,timeslice1)

    !!replace by convert
    !!timeslice1%phasepoint=startpoint
    !!phasexv=timeslice1%phasepoint%phasexv
    !!call prepare_MDinout_param(timeslice1%MDinout_param,phasexv,syst,pot,dyn)
    !!timeslice1%OPS(1)=orderp(phasexv%x,syst)

    reverseTF=.false.
    print *,"SEARCHING CROSSING WITH THE MIDDLE INTERFACE.."
    
    do 
       call kick_timeslice(timeslice1,syst,pot,dyn,TIS_param%sigdp_sqrtm,&
                           aimless=.true.) 
       timeslice2=timeslice1
       
       call propagate(timeslice2,syst,pot,dyn,timestep,reverseTF)  
       
       
       OP1=timeslice1%OPS(1);OP2=timeslice2%OPS(1)
       print *,"OP1, OP2, INTERFACEM",real(OP1), real(OP2), real(INTERFACEM)

       if ((OP1<=INTERFACEM).AND.(OP2>INTERFACEM)) exit
       if ((OP1>=INTERFACEM).AND.(OP2<INTERFACEM)) exit
       if (((OP1<INTERFACEM).AND.(OP2>=OP1)).OR. &
          ((OP1>INTERFACEM).AND.(OP2<=OP1)))&
          timeslice1=timeslice2            !getting closer
    enddo 
    print *,"SUCCEEDED. GOING FORWARD IN TIME.."
    

    LMAX=NX-1
    call onewaytraj(timeslice2,syst,pot,dyn,timestep,reverseTF,INTERFACEL,& 
                  INTERFACER,LMAX,trajsegf,wt1)
    print *,"FINISHED"

    print *,"GOING BACKWARD IN TIME"
    LPF=trajsegf%Lpath
    reverseTF=.true.
    LMAX=NX-LPF
    call onewaytraj(timeslice1,syst,pot,dyn,timestep,reverseTF,INTERFACEL,&
                  INTERFACER,LMAX,trajsegb,wt1)
    print *,"FINISHED"
    LPB=trajsegb%Lpath
  
    call paste(trial,trajsegb,trajsegf,interfaceL,interfaceM,interfaceR,&
               overlap=.false.)
    Lpath=trial%Lpath
    if (LPATH==NX) then
       print *,"INITIALIZATION FAILED: Lpath > NX"
       stop
    endif
    startpath=trial

    if ((startcon/=" ").AND.(trial%start/=startcon)) then
        if (trial%end/=startcon) then
          print *,"PATH ENDS AT BOTH SIDES AT WRONG INTERFACE",trial%start
          print *, "lmin,lmax",trial%opmin,trial%opmax
          print *, "starting loop"
          TIS_param%startcondition=" "
          do 
            call make_TISstep(startpath,tis_param,syst,pot,dyn,timestep,&
                              trial,wp1,wp2,wt1,wt2,allowmaxlength)
            if ((startcon=="R").AND.(trial%opmax>startpath%opmax).AND.(trial%opmin<interfaceM)) then
              startpath=trial
              print *, "lmin,lmax",startpath%opmin,startpath%opmax
              if (startpath%opmax>interfaceR) exit
            else if ((startcon=="L").AND.(trial%opmin<startpath%opmin).AND.(trial%opmax>interfaceM)) then
              startpath=trial
              print *, "lmin,lmax",startpath%opmin,startpath%opmax
              if (startpath%opmin<interfaceL) exit
            endif
          enddo
          TIS_param%startcondition=startcon
        endif 
        if (trial%start/=startcon) then
          print *,"STARTING AT WRONG INTERFACE"
          print *,"REVERSING PATH ...."
          call timereversal(trial,startpath,TIS_param%startcondition)
        endif
    endif 
    startpath%index_acc=0
    startpath%index_shoot=0
    startpath%OPshoot=OP1
    startpath%iOPshoot_old=0
    startpath%iOPshoot_new=LPB
    startpath%MCmove="sh"

    print *,"INITIALIZATION STARTPATH SUCCESFULLY COMPLETED"
    startpath%index_acc=0
    startpath%index_shoot=0
    startpath%OPshoot=OP1
    startpath%iOPshoot_old=0
    startpath%iOPshoot_new=LPB
    startpath%MCmove="sh"

  endif
 
    !deallocate everything except startpath and TISparam 
    call dealloc(startpoint)          ! (was allocated in initMD)
    call dealloc(output)              ! (was allocated in initMD)
    call dealloc(timeslice1)
    call dealloc(timeslice2)
    call dealloc(phasexv)
    call dealloc(trajsegb)
    call dealloc(trajsegf)
    call dealloc(trial)
    call dealloc(wp1)
    call dealloc(wp2)
    call dealloc(wt1)
    call dealloc(wt2)
    !startpath and TISparam are still allocated
    !the following files are opened without closing:
    !output%IUPATH:=output%PATH_FILE)
    !output%IUEN:=output%ENERGY_FILE, output%IUTRAJ:=output%TRAJECTORY_FILE
    !output%IUOP:=ORDER_PARAM_FILE, output%IUCR:=CROSSFILE
    !The files in the last two lines were opened in initMD
    !Thus beware when init_TIS is called inside a loop!
 
  end subroutine init_TIS
  !ES------------------------


end Module initTIS
!EM--------------------------
